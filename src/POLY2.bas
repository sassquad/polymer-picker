REM Polymer Picker
?&36D=&B:MODE2:VDU23;8202;0;0;0;
Q%=RND(-TIME)
PROCinit
REPEAT
PROCscreen
PROCstart
PROCboat
IF(lev%MOD2)=1PROCfish ELSE PROCshark
REMPROCshark
PROCbags
PROCstatus
PROCairbar
TIME=0
REPEAT
PROCdiver
PROCair
IF(lev%MOD2)=1PROCmovefish ELSE PROCmoveshark
REMPROCmovefish
REMPROCmoveshark
UNTILBAG%=0 OR DEAD%=0 OR O%<912
IFBAG%=0 lev%=lev%+1
IFDEAD%=0 PROCover
IFO%<912 PROCover
UNTIL0
DEFPROCinit
DIM FX%(7),FY%(7),FH%(7),FV%(7),F%(7)
DIM BX%(7),BY%(7),B%(7),sky%(3):lev%=1
FORa%=0TO3
READ sky%(a%)
NEXT
DATA 134,133,128,131
ENDPROC
DEFPROCscreen
VDU4:COLOUR132:CLS:VDU19,14,6;0;
VDU28,0,4,19,0,17,sky%((lev%-1)MOD4),12,5
IF(lev%MOD4)=1:PROCcirc(40,980,40,3):PROCmt(864,200,64,2):PROCmt(980,200,64,2)
IF(lev%MOD4)=2:PROCcirc(40,860,40,1):PROCmt(864,180,32,0):PROCmt(980,200,64,0)
IF(lev%MOD4)=3:PROCcirc(100,920,40,7):PROCcirc(120,920,40,0):PROCmt(864,180,32,7):PROCmt(980,200,64,7)
IF(lev%MOD4)=0:PROCcirc(40,860,40,5):PROCmt(864,120,32,1):PROCmt(980,200,64,1)
VDU4,26,28,0,31,19,27,17,128,12,26,28,0,28,19,27,17,131,12,5
FORT%=200TO205:VDU23,T%:FORI%=1TO8:VDURND(255):NEXT,
FORY%=127TO160STEP32
FORX%=0TO1279STEP64 
MOVE X%,Y%
VDU18,3,7,RND(6)+199
NEXT,
ENDPROC
DEFPROCstart
FORG=0TO9:MOVEFNrx,224:VDU18,0,2,243,10,8,242:NEXT
FORG=0TO9:MOVEFNrx,224:VDU18,0,5,32,10,8,253:NEXT
X%=FNrx:Y%=864:Z%=1
MOVEX%,Y%:PROCP(Z%):PROCb(X%,Y%)
BW%=63:BH%=31:V%=16:H%=16:BAG%=8:DEAD%=8:O%=1224:OD%=200:SF%=16:DF%=1
ENDPROC
DEFPROCboat
VDU5:GCOL0,0:MOVEFNrx,892:PRINT"\###/"
ENDPROC
DEFPROCshark
SX%=FNrx:SY%=FNry
IFSX%<=X% PROCsr ELSE PROCsl
VDU5,18,3,3:MOVE SX%,SY%
VDU192,193,194,195,8,8,8,8,10,196,197,198,199
ENDPROC
DEFPROCsl
VDU23,192,0,0,0,0,224,252,127,103,23,193,0,0,1,3,7,31,255,255,23,194,64,192,128,0,0,0,0,224,23,195,0,0,0,0,0,1,3,6
VDU23,196,55,62,14,6,31,0,0,0,23,197,255,191,187,181,238,7,193,64,23,198,248,255,255,255,254,4,192,64,23,199,204,140,252,252,204,70,2,1
ENDPROC
DEFPROCsr
VDU23,192,0,0,0,0,0,128,64,96,23,193,2,3,1,1,0,0,0,7,23,194,0,0,128,192,224,248,255,255,23,195,0,0,0,0,7,63,254,230
VDU23,196,115,49,63,63,51,98,64,128,23,197,31,255,255,255,127,32,3,2,23,198,255,253,221,173,119,224,131,2,23,199,236,124,112,96,248,0,0,0
ENDPROC
DEFPROCfish
VDU5,18,3,2
FORA=0TO7:FX%(A)=FNrx:FY%(A)=FNry:FH%(A)=H%:F%(A)=241
IF A>3 THEN FH%(A)=-H%:F%(A)=240
FV%(A)=V%:MOVE FX%(A),FY%(A):VDU F%(A)
NEXT
E%=0:F%=-4
ENDPROC
DEFFNrx:=(RND(32)+2)*32
DEFFNry:=(RND(33)+18)*16
DEFPROCbags
IF(lev%MOD4)=1:VDU23,251,8,24,56,126,255,62,28,8
IF(lev%MOD4)=2:VDU23,251,120,252,255,255,255,252,120,0
IF(lev%MOD4)=3:VDU23,251,102,153,153,126,126,153,153,102
IF(lev%MOD4)=0:VDU23,251,66,66,126,126,126,126,126,126
VDU5,18,3,5
FORA=0TO7
BX%(A)=FNrx
BY%(A)=FNry
MOVE BX%(A),BY%(A)
VDU 251
NEXT
ENDPROC
DEFPROCairbar
VDU5:GCOL0,14:MOVE920,40:MOVE1216,40:PLOT85,920,52:PLOT85,1216,52
GCOL0,7:MOVE912,36:DRAW1224,36:DRAW1224,56:DRAW912,56:DRAW912,36
ENDPROC
DEFPROCair
IFTIME>OD%:O%=O%-8:SOUND0,4,6,3:IFO%>912 GCOL0,0:MOVEO%,52:DRAWO%,40:TIME=0
IFO%>1000 VDU19,14,6;0; ELSE VDU19,14,9;0;
ENDPROC
DEFPROCmoveshark
OSX%=SX%:OSY%=SY%
IFSX%<=X%ANDSX%<1216 THEN SX%=SX%+SF%
IFSX%>X%ANDSX%>0 THEN SX%=SX%-SF%
IFSY%<Y%ANDSY%<800 THEN SY%=SY%+SF%
IFSY%>Y%ANDSY%>192 THEN SY%=SY%-SF%
IFSX%<>OSX% OR SY%<>OSY% PROCps
ENDPROC
DEFPROCps
MOVEOSX%,OSY%:VDU18,3,3,192,193,194,195,8,8,8,8,10,196,197,198,199
IFSX%<=X% PROCsr ELSE PROCsl
MOVESX%,SY%:VDU18,3,3,192,193,194,195,8,8,8,8,10,196,197,198,199
ENDPROC
DEFPROCmovefish
IF FH%(E%)=0 AND FY%(E%)=176 THEN E%=(E%+1) MOD 8:ENDPROC
IF FH%(E%)<>0 THEN GCOL3,2 ELSE GCOL3,5
MOVE FX%(E%),FY%(E%):VDU F%(E%)
FX%(E%)=FX%(E%)+FH%(E%)
FY%(E%)=FY%(E%)+FV%(E%)
IF FX%(E%)<0 THEN FH%(E%)=H%:F%(E%)=241
IF FX%(E%)>1216 THEN FH%(E%)=-H%:F%(E%)=240
IF FY%(E%)<192 AND FH%(E%)<>0 THEN FV%(E%)=V% ELSE IF FY%(E%)<192 AND FH%(E%)=0 THEN FY%(E%)=176
IF FY%(E%)>800 AND FH%(E%)<>0 THEN FV%(E%)=-V%
IF FH%(E%)<>0 THEN GCOL3,2 ELSE GCOL3,5
MOVE FX%(E%),FY%(E%):VDU F%(E%)
CFX%=FX%(E%)+16:CFY%=FY%(E%)-40
IF FH%(E%)<>0 AND POINT(CFX%,CFY%)=1:PROCfishcheck(CFX%,CFY%,E%)
E%=(E%+1) MOD 8
ENDPROC
DEFPROCdiver
PROCair
A%=X%:B%=Y%:C%=Z%
IFINKEY-67ANDX%<1080:X%=X%+(DF%*32):Z%=1
IFINKEY-98ANDX%>0:X%=X%-(DF%*32):Z%=-1
IFINKEY-73ANDY%<864:Y%=Y%+(DF%*16)
IFINKEY-105ANDY%>224:Y%=Y%-(DF%*16)
IFINKEY-74AND(X%<>A% OR Y%<>B%):DF%=2:OD%=10:PROCair:ELSE:DF%=1:OD%=200
IFX%<>A% OR Y%<>B% MOVEA%,B%:PROCP(C%):PROCb(A%,B%):MOVEX%,Y%:PROCP(Z%):PROCb(X%,Y%)
CX%=X%+88
CY%=Y%-56
IFPOINT(CX%,CY%)=1:PROCcheck(CX%,CY%)
IFPOINT(CX%,CY%)=5 OD%=50:PROChurt ELSE OD%=200
ENDPROC
DEFPROChurt
PROCair:SOUND1,-15,255,1
ENDPROC
DEFPROCP(f%)
VDU18,3,3
IFf%=1VDU232,224,225,8,8,8,10,233,226,227ELSEVDU228,229,234,8,8,8,10,230,231,235
ENDPROC
DEFPROCb(a%,b%)
VDU18,3,15
MOVEa%+64,b%+32:VDU254
VDU18,3,8
MOVEa%+76,b%+48:VDU255
ENDPROC
DEFPROCcheck(CX%,CY%)
C%=0
REPEAT
I%=CX%<BX%(C%) OR CY%>BY%(C%) OR CX%>BX%(C%)+BW% OR CY%<BY%(C%)-BH%
C%=C%+1
UNTIL C%>7 OR I%=0
IF I%=FALSE THEN BAG%=BAG%-1:SOUND1,1,99,1:VDU18,3,5:MOVEBX%(C%-1),BY%(C%-1):VDU251:SOUND0,-10,150,1:BX%(C%-1)=-10:BY%(C%-1)=-10
PROCstatus
ENDPROC
DEFPROCfishcheck(CFX%,CFY%,E%)
C%=0
REPEAT
I%=CFX%<BX%(C%) OR CFY%>BY%(C%) OR CFX%>BX%(C%)+BW% OR CFY%<BY%(C%)-BH%
C%=C%+1
UNTIL C%>7 OR I%=0
IF I%=FALSE THEN PROCdeadfish(E%):VDU18,3,5:MOVEBX%(C%-1),BY%(C%-1):VDU251:SOUND0,-10,150,1:BX%(C%-1)=-10:BY%(C%-1)=-10
ENDPROC
DEFPROCdeadfish(E%)
BAG%=BAG%-1
DEAD%=DEAD%-1
SOUND0,-10,50,10
GCOL3,2
MOVE FX%(E%),FY%(E%):VDU F%(E%)
GCOL3,5
MOVE FX%(E%),FY%(E%):VDU F%(E%)
FH%(E%)=0:FV%(E%)=-32
PROCstatus
ENDPROC
DEFPROCstatus
VDU26,4,17,128,17,1,31,1,30,251,32,17,7:PRINT;BAG%
VDU31,6,30,17,6,241,32,17,7:PRINT;DEAD%
VDU31,11,30:PRINT"Air":VDU5
ENDPROC
DEFPROCover
VDU4,31,5,2
IFBAG%<DEAD%PRINT"NICE!" ELSE PRINT"POOR!"
VDU31,2,3
PRINT"Spacebar to play"
*FX15
REPEATUNTILGET=32
lev%=1:ENDPROC
DEFPROCcirc(h%,v%,r%,c%)
VDU24,0;864;1279;1023;
VDU29,h%;v%;:GCOL0,c%
x%=r%:y%=0:z%=x%/2
REPEAT
MOVEx%,y%:PLOT5,-x%,y%
MOVEy%,x%:PLOT5,-y%,x%
MOVEx%,-y%:PLOT5,-x%,-y%
MOVEy%,-x%:PLOT5,-y%,-x%
y%=y%+1:z%=z%-y%
IF z%<0 THEN z%=z%+x%:x%=x%-1
UNTIL x%<y%
ENDPROC
DEFPROCmt(X%,LE%,HE%,CO%)
VDU29,X%;864;
MOVE0,0:L=LE%/PI:OX%=-32:OY%=0
FORx%=0TOLE%STEP64
y%=40+RND(40)+HE%*SIN(x%/L)
GCOL0,CO%:MOVEx%,y%:MOVEx%,0
PLOT85,OX%,OY%:PLOT85,OX%,0
GCOL0,0:MOVEOX%,OY%+4:DRAWx%,y%+4
OX%=x%:OY%=y%
NEXT
DRAWx%+16,4
GCOL0,CO%:MOVEOX%,0:MOVEOX%,OY%
PLOT85,x%+16,0
ENDPROC
